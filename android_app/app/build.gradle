apply plugin: 'com.android.application'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.health.openscale"
        testApplicationId "com.health.openscale.test"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 31
        versionName "1.8.3"

        javaCompileOptions {
            annotationProcessorOptions { arguments = ["room.schemaLocation":"$projectDir/schemas".toString()] }
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    implementation 'com.google.android.material:material:1.1.0-alpha01'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.1'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'

    // HelloCharts
    implementation 'com.github.lecho:hellocharts-library:1.5.8@aar'

    // Simple CSV
    implementation 'com.j256.simplecsv:simplecsv:2.3'

    // RxAndroidBle
    implementation "com.polidea.rxandroidble2:rxandroidble:1.7.1"
    implementation 'io.reactivex.rxjava2:rxjava:2.1.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'com.jakewharton.rx2:replaying-share:2.1.0'

    // CustomActivityOnCrash
    implementation 'cat.ereza:customactivityoncrash:2.2.0'

    // Room
    implementation 'androidx.room:room-runtime:2.1.0-alpha02'
    annotationProcessor 'androidx.room:room-compiler:2.1.0-alpha02'
    androidTestImplementation 'androidx.room:room-testing:2.1.0-alpha02'

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.0'

    // Local unit tests
    testImplementation 'junit:junit:4.12'

    // Instrumented unit tests
    androidTestImplementation 'androidx.annotation:annotation:1.0.0'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test:rules:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.1.0'
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams true
    }
}
